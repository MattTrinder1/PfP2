App As appinfo:
    BackEnabled: =false
    OnStart: |
        =Set(
            glbAppVersion,
            1
        );
        Set(
            glbDebug,
            true
        );
        //this value is a placeholder which is updated by the build pipeline, don't edit it manually
        //its commented it out to avoid it being formatted, which prevents the pipeline from finding it
        //Basically - leave it alone !
        //Set(glbAppVersion,1);
        //Set(glbDebug,false);
        
        
        Set (
            glbOnline,
            IfError(
                AzureQueues.PutMessage(
                    "onlinecheckqueue",
                    User().Email
                ),
                false,
                true
            )
        );
        
        Collect(
            notePhotos,
            {
                noteId: GUID(),
                id: "",
                caption: "",
                index: 0,
                blob: SampleImage,
                downloaded:false
            }
        );
        Clear(notePhotos);
        LoadData(notePhotos,"notePhotosCache",true);
        
        Set (glbInTimerLoop,false);
        
        Set (
            nullDate,
            DateTimeValue("1900-01-01 00:00:00")
        );
        Set(tmrBlobSaveStart,false);
        Set(tmrBlobSaveStart,true);
        Set(tmrNoteValidateStart,false);
        Set(tmrNoteValidateStart,true);
        Set(tmrNoteSaveStart,false);
        Set(tmrNoteSaveStart,true);
        Set(tmrNotePopulateStart,false);
        Set(tmrNotePopulateStart,true);
        
        IfError(
            LoadData(
                'Notebook Offline',
                "NotebookOfflineCache",
                true
            ),
            true
        );
        LoadData(
            blobsToSave,
            "blobsToSaveCache",
            true
        );
        
        Set(
            gblLoading,
            false
        );
        Set(
            gblLoading2,
            false
        );

